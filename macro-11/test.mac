;
;  Test program to test various library routines.
;
	.INCLUDE /$COMMON.MAC/
	.INCLUDE /MACROS.MAC/
	.PSECT CODE,SAV,CON,GBL,I
	.TITLE TEST Program to test various libarary routines
	.IDENT	"X00.00"
	.ENABL GBL
;
	.SBTTL start - Entry point to print message
	text hello, <Macro-11 Test Program>
	text prompt, <CMD: >
	text dot, <.>
	string str, 80.
start::
	JSR PC, init
	BIS #flglz+flgsgn, flgnum
	putsln #hello
;
cloop:	puts #prompt			; Ask for a string
	gets #str			; Get the string
	trim #str,ts=1,lz=1,ls=1	; Trim the string
	upcase #str
	sfindc #str, #32., R1
	substr #str, #cmd, #0, R1
	substr #str, #rest, R1, #0
	trim #rest, ls=1
;
;  Check for commands
;
	MOV #tblcmd, R0
100$:	TST (R0)
	BNE 101$
	JMP nocmd
101$:	streq #cmd, (R0)+
	BEQ 102$
	TST (R0)+
	BR 100$
102$:	MOV (R0), PC
;
;  No recognized command given
;
nocmd:	cpstr #firstw, #outbuf
	appstr #cmd, #outbuf
	appstr #vbar, #outbuf
	putsln #outbuf
;
	cpstr #trest, #outbuf
	appstr #rest, #outbuf
	appstr #vbar, #outbuf
	putsln #outbuf
;
	strchr #str, #5, R0
	chrstr #temp, R0
	cpstr #msg2, #outbuf
	appstr #temp, #outbuf
	appstr #vbar, #outbuf
	putsln #outbuf
;
	upcase #str
	puts #msg2
	puts #str
	putsln #vbar		; Put the uppercase string
	locase #str
	puts #msg2
	puts #str
	putsln #vbar		; Put the lowercase string
;
	puts #msg4		; Print the string's max len
	strmax #str, R0
	putdec R0
	JSR PC, newline
	puts #msg5		; Print the string's actual len
	strlen #str, R0
	putdec R0
	JSR PC, newline
;
	JMP cloop
;
octal:	stroct #rest, R1
	JSR PC, putnm1
	JMP cloop
;
deci:	strdec #rest, R1
	JSR PC, putnm1
	JMP cloop
;
hexi:	strhex #rest, R1
	JSR PC, putnm1
	JMP cloop
;
eerr:	strdec #rest, R1
	geterr R1, #outbuf
	putsln #outbuf
	JMP cloop
;
ename:	setnam #rest
	JMP cloop
;
esleep:	strdec #rest, R1
	sleep R1
	JMP cloop
;
estar:	strdec #rest, R1
	repchr #str, #'*, R1
	puts #thestr
	puts #str
	putsln #vbar
	JMP cloop
;
estack:	MOV R0, -(SP)
	MOV SP, R0
	cpstr #tstack, #outbuf
	octstr #str, R0
	appstr #str, #outbuf
	putsln #outbuf
	MOV (SP)+, R0
	JMP cloop
;
	.SBTTL Loop - To print numbers
list:	BIC #flglz+flgsgn, flgnum
	cpstr #rest, #str
	sfindc #str, #32., R1
	substr #str, #cmd, #0, R1
	substr #str, #rest, R1, #0
	trim #cmd,ls=1
	trim #rest,ls=1
	strdec #cmd, R1
	strdec #rest, R0
;
1$:	JSR PC, putnm1
	INC R1
	SOB R0, 1$
	JMP cloop
;
efirqb:	cpstr #thestr, #outbuf
	cvtfqb #cfnam+cfext+cfppn+cfdev, #outbuf
	appstr #vbar, #outbuf
	putsln #outbuf
	JMP cloop
;
efqerr:	MOV @#firqb, R0
	geterr R0, #outbuf
	putsln #outbuf
	JMP cloop
;
eparsf:	parsef #rest
	MOV @#firqb, R0
	BEQ 1$
	geterr R0, #outbuf
	putsln #outbuf
1$:	JMP cloop
;
done:	JMP exit
;	.EXIT
;
efilst:	CLR R0
100$:	MOV R0, R1
	cpstr #chan, #outbuf
	decstr #temp, R1
	appstr #temp, #outbuf
	appstr #is, #outbuf
	MOV R1, -(SP)
	JSR PC, chanck
	MOV (SP)+, R1
	CMPB #notopn, R1	; Check for not open
	BNE 2$
	appstr #nopn, #outbuf
	BR 1$
2$:	CMPB #nodevc, R1	; Check for not disk
	BNE 3$
	appstr #opn, #outbuf
	BR 1$
3$:	CMPB #bserr, R1		; Check for invalid chan
	BNE 4$
	appstr #inv, #outbuf
	BR 1$
4$:	TST R1
	BEQ 5$
	appstr #unk, #outbuf
	decstr #temp, R1
	appstr #temp, #outbuf
	BR 1$	
5$:	appstr #dsk, #outbuf
1$:	putsln #outbuf
	INC R0
	CMP #16., R0
	BNE 100$
	JMP cloop
	text chan, <Channel >
	text is, < is >
	text dsk <open as a disk file>
	text opn <open an a non-disk file>
	text nopn, <not open>
	text inv, <an invalid channel>
	text unk, <unknown status >
;
;  Prints a number in octal, decimal, or hex.  The number is
;  passed in R1.  This is done by building a string and then
;  printing it all at once.
;
	text sgnum, <  The signed number is >
	text unum,  <The unsigned number is >
	text oct, < Octal, >
	text dec, < Decimal, >
	text hex, < Hexidecimal.>
putnm1:	BIS #flgsgn, flgnum
	cpstr #sgnum, #outbuf
	octstr #temp, R1
	appstr #temp, #outbuf
	appstr #oct, #outbuf
	decstr #temp, R1
	appstr #temp, #outbuf
	appstr #dec, #outbuf
	hexstr #temp, R1
	appstr #temp, #outbuf
	appstr #hex, #outbuf
	putsln #outbuf
	TST R1
	BGE 1$
	BIC #flgsgn, flgnum
	cpstr #unum, #outbuf
	octstr #temp, R1
	appstr #temp, #outbuf
	appstr #oct, #outbuf
	decstr #temp, R1
	appstr #temp, #outbuf
	appstr #dec, #outbuf
	hexstr #temp, R1
	appstr #temp, #outbuf
	appstr #hex, #outbuf
	putsln #outbuf
1$:	RTS PC
;
	.SBTTL Data - Space for text messages and strings
	string outbuf, 80.
	string temp, 80.
	string cmd, 80.
	string rest, 80.
	text enttxt, <Enter text: >
	text msg2, <You entered: |>
	text vbar, <|>
	text msg4, <The string max len is: >
	text msg5, <The string length is:  >
	text thestr, <The string is |>
	text firstw, <The first word |>
	text trest, <The rest is |>
	text tstack, <Current SP is >
;
;  Text for commands
;
	text cexit, <EXIT>
	text coct, <OCTAL>
	text cdec, <DEC>
	text chex, <HEX>
	text clist, <LIST>
	text cstack, <STACK>
	text cerr, <ERROR>
	text cname, <NAME>
	text csleep, <SLEEP>
	text cstar, <STARS>
	text cfirqb, <FIRQB>
	text cfqerr, <FQERR>
	text cparsf, <PARSE>
	text cfilst, <FILE-LIST>
;
;  Command jump table
;
	.PSECT DATA,RW,D,LCL,REL,CON
tblcmd:	.WORD cexit,done
	.WORD coct,octal
	.WORD cdec,deci
	.WORD chex,hexi
	.WORD clist,list
	.WORD cstack,estack
	.WORD cerr,eerr
	.WORD cname,ename
	.WORD csleep,esleep
	.WORD cstar,estar
	.WORD cfirqb,efirqb
	.WORD cfqerr,efqerr
	.WORD cparsf,eparsf
	.WORD cfilst,efilst
	.WORD 0
;
	.END start
